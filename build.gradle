apply plugin:'idea'
apply plugin:'groovy'
apply plugin:'maven'

group = 'dlna-server'
version = System.getenv('VERSION') ?: 'dev-SNAPSHOT'
archivesBaseName = 'fitnesse'
sourceCompatibility = 1.6

configurations {
    deployerJars
    tests 
    published.extendsFrom tests, deployerJars, archives
}

repositories {
    mavenCentral()
    maven {
        credentials {
            username = NEXUS_USER
            password = NEXUS_PASS
        }
        url "http://nexus.bonos.be:9090/content/groups/public"
    }
}

task sourcesJar(type: Jar) { 
    classifier = 'sources' 
    from sourceSets.main.allSource 
}

task testJar(type: Jar) { 
    classifier = 'tests' 
    from sourceSets.test.output 
}

artifacts { 
    archives sourcesJar, testJar 
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            def mainPom = addFilter('main') {artifact, file -> !artifact.name.endsWith("-tests") }
            def testsPom = addFilter('tests') {artifact, file -> artifact.name.endsWith("-tests") }

            def poms = [ mainPom, testsPom ]
            poms.each { it.version = project.version }

            def auth = {
                authentication(userName:NEXUS_USER, password:NEXUS_PASS)
            }
            repository url:"http://nexus.bonos.be:9090/content/repositories/releases", auth
        }
    }
}

install { 
    configuration = configurations.published 
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:1.8.6'
    compile "org.spockframework:spock-core:0.6-groovy-1.8", {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    deployerJars "org.apache.maven.wagon:wagon-http:1.0-beta-2"
}
